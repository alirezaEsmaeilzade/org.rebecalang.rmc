reactiveclass ResourceManager(5)
{
	knownrebecs
	{
		AppMaster am1;
		AppMaster am2;
		AppMaster am3;
	}
	
	statevars
	{
		int FREE;
		int BUSY;
		int HIGH;
		int LOW;
		int appMaster1;
		int appMaster2;
		int appMaster3;
		int m_queue_misses; //Were deadlines in the queue missed
		int m_update_miss; //Was a deadline missed while a job was running
		int m_job_complete; //Did a job complete within the deadline
		int[3] deadlines;
		int[3] priority_queue;
		int DEFAULT_DEADLINE;
		int QUEUE_SIZE;
	}
	
	ResourceManager()
	{
		FREE = 1;
		BUSY = 0;
		HIGH = 1;
		LOW = 0;
		appMaster1 = FREE;
		appMaster2 = FREE;
		appMaster3 = FREE;
		m_queue_misses = 0;
		m_update_miss = 0;
		m_job_complete = 0;
		DEFAULT_DEADLINE = 3;
		deadlines[0] = DEFAULT_DEADLINE;
		deadlines[1] = DEFAULT_DEADLINE;
		deadlines[2] = DEFAULT_DEADLINE;
		priority_queue[0] = LOW;
		priority_queue[1] = LOW;
		priority_queue[2] = LOW;
		QUEUE_SIZE = 3;
		self.checkQueue();
	}
	
	msgsrv checkQueue()
	{
		//1a. if an appmaster is free, send him the first high priority job in the queue,
		//else the n
		//1b. shift the queue and add a new job at the back
		//2. decrement all the deadlines in the queue
		m_queue_misses = 0;
		m_update_miss = 0;
		m_job_complete = 0;
		int I = 0;
		if(appMaster1 == FREE)
		{
			int brk = 0;
			while(I < QUEUE_SIZE && brk == 0)
			{
				if(priority_queue[I] == HIGH)
				{
					brk = 1;
				}
				I++;
			}
			appMaster1 = BUSY;
			am1.runJob(deadlines[I]);
			while(I < QUEUE_SIZE - 1)
			{
				deadlines[I] = deadlines[I + 1];
				priority_queue[I] = priority_queue[I + 1];
				I++;
			}
		}
		if(appMaster2 == FREE)
		{
			int brk = 0;
			while(I < QUEUE_SIZE && brk == 0)
			{
				if(priority_queue[I] == HIGH)
				{
					brk = 1;
				}
				I++;
			}
			appMaster2 = BUSY;
			am2.runJob(deadlines[I]);
			while(I < QUEUE_SIZE - 1)
			{
				deadlines[I] = deadlines[I + 1];
				priority_queue[I] = priority_queue[I + 1];
				I++;
			}
		}
		if(appMaster3 == FREE)
		{
			int brk = 0;
			while(I < QUEUE_SIZE && brk == 0)
			{
				if(priority_queue[I] == HIGH)
				{
					brk = 1;
				}
				I++;
			}
			appMaster3 = BUSY;
			am3.runJob(deadlines[I]);
			while(I < QUEUE_SIZE - 1)
			{
				deadlines[I] = deadlines[I + 1];
				priority_queue[I] = priority_queue[I + 1];
				I++;
			}
		}
		I = 0;
		int J = 0;
		while(I < QUEUE_SIZE)
		{
			deadlines[I]--;
			if(deadlines[I] == 0)
			{
				m_queue_misses++;
				J = I;
				while(J < QUEUE_SIZE - 1) //Shift the queue and put a new job in the back
				{
					deadlines[J] = deadlines[J + 1];
					priority_queue[J] = priority_queue[J + 1];
					J++;
				}
				deadlines[QUEUE_SIZE - 1] = DEFAULT_DEADLINE;
				priority_queue[QUEUE_SIZE - 1] = ?(0, 1);
			}
			I++;
		}
		self.checkQueue() after(1);
	}
	
	msgsrv update(boolean deadline_miss)
	{
		m_queue_misses = 0;
		m_update_miss = 0;
		m_job_complete = 0;
		if(deadline_miss == true)
		{
			m_update_miss = 1;
		}
		else
		{
			m_job_complete = 1;
		}
		if(sender == am1)
		{
			appMaster1 = FREE;
		}
		else if(sender == am2)
		{
			appMaster2 = FREE;
		}
		else if(sender == am3)
		{
			appMaster3 = FREE;
		}
	}
}

reactiveclass AppMaster(5)
{
	knownrebecs
	{
		ResourceManager rm;
	}
	
	statevars
	{
	
	}
	
	AppMaster()
	{
	
	}
	
	msgsrv runJob(int dline)
	{
		int completion = ?(2, 5);
		boolean deadline_miss;
		if(completion > dline)
		{
			deadline_miss = true;
			//Stops the job if deadline passes (self-preemption)
			rm.update(deadline_miss) after(dline);
		}
		else
		{
			deadline_miss = false;
			//Informs the rm as soon as the job completes
			rm.update(deadline_miss) after(completion);
		}
	}
}

main
{
	ResourceManager rm(am1, am2, am3):();
	AppMaster am1(rm):();
	AppMaster am2(rm):();
	AppMaster am3(rm):();
}